buildscript {

	repositories {
		mavenCentral()
		jcenter()
	}
}

subprojects {

	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'

	group 'net.osmand'
	version 'master-snapshot'
	sourceCompatibility = 11
	eclipse {
    	   classpath {
	        file {
        	    whenMerged {
                	entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'false' }
            	    }
        	}
    	   }
	}
 

	test {
		reports {
			junitXml.enabled = true
			html.enabled = false
		}
	}

	task fatJar(type: Jar) {
		
		manifest.from jar.manifest
		dependsOn configurations.runtimeClasspath
		if(project.hasProperty('mainClassAttribute')) {
			manifest.attributes 'Main-Class': project.getProperty('mainClassAttribute');
		}
		archiveName "${project.name}.jar"
		from {
			configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
		} 
		{
			exclude "META-INF/*.SF"
			exclude "META-INF/*.DSA"
			exclude "META-INF/*.RSA"
		}
		with jar
	}


	repositories {
		maven {
			url "https://maven.geotoolkit.org"
		}
		maven {
			url "https://maven.geo-solutions.it/"
		}
		mavenCentral()
		jcenter()
		google()
		
	}
}
