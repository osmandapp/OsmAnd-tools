jar {
    doFirst {
		manifest {
        	attributes('Main-Class': project.getProperty('mainClassAttribute'),
        		   'Class-Path': configurations.runtimeClasspath.collect { 'lib/'+it.getName() }.join(' ')
        		   )
    	}
    }
}

task collectMdpiIcons(type: Copy) {
	from("../../../resources/rendering_styles/style-icons/map-shaders-png/drawable-mdpi")
	into "src/main/resources/resources"
}

task collectMdpiIcons2(type: Copy) {
	from("../../../resources/rendering_styles/style-icons/map-icons-png/drawable-mdpi")
	into "src/main/resources/resources"
}

task collectFonts(type: Copy) {
	from("../../../resources/rendering_styles/fonts"){
		include "05_NotoSans-Regular.ttf"
		include "10_NotoSans-Bold.ttf"
		include "15_NotoSans-Italic.ttf"
		include "20_NotoSans-BoldItalic.ttf"
		include "25_NotoSansArabic-Regular.ttf"
		include "30_NotoSansArabic-Bold.ttf"
		include "35_NotoSansSouthAsian-Regular.ttf"
		include "40_NotoSansSouthAsian-Bold.ttf"
		include "45_NotoSansSoutheastAsian-Regular.ttf"
		include "50_NotoSansSoutheastAsian-Bold.ttf"
		include "55_NotoSansTibetan-Regular.ttf"
		include "60_NotoSansTibetan-Bold.ttf"
		include "65_NotoSansNastaliqUrdu-Regular.ttf"

		include "DroidSansFallback.ttf"
		include "MTLmr3m.ttf"
	}

	into "fonts"	
}

task buildDistribution(type: Zip) {
	dependsOn jar
   	from(configurations.runtime.allArtifacts.files) {
   		rename { it.contains("OsmAnd") ? it.replace("-1.0", '') : it }
//   		into '.'
   	}

   	from('lib-native') {
   		into 'lib'
   	}

   	from(configurations.runtimeClasspath) {
   		into 'lib'
   	}

   	from('fonts') {
   		into 'fonts'
   	}
   	from('src/main/resources') {
   		include '*.sh'
   		include '*.bat'
   		include 'batch.xml'
   		include 'logging.properties'
   	}
	archiveName 'OsmAndMapCreator.zip'
}

compileJava {
    dependsOn collectMdpiIcons, collectMdpiIcons2, collectFonts
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation project(':OsmAnd-java')
    implementation project(':OsmAndMapCreatorUtilities')

    implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
    implementation 'org.apache.commons:commons-compress:1.17'
    implementation 'net.sf.kxml:kxml2:2.1.8'
    implementation group: 'org.json', name: 'json', version: '20171018' 
    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
 	
 	  implementation fileTree(dir: "lib-gl", include: ["*.jar"])

 	  implementation "org.jogamp.gluegen:gluegen-rt:2.3.1"
    implementation "org.jogamp.jogl:jogl-all:2.3.1"

    // runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-android-aarch64"
    // runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-android-armv6"
    runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-linux-amd64"
    runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-linux-armv6"
    // runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-linux-armv6hf"
    runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-linux-i586"
    runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-macosx-universal"
    runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-solaris-amd64"
    runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-solaris-i586"
    runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-windows-amd64"
    runtime "org.jogamp.gluegen:gluegen-rt:2.3.1:natives-windows-i586"

    // runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-android-aarch64"
    // runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-android-armv6"
    runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-linux-amd64"
    runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-linux-armv6"
    // runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-linux-armv6hf"
    runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-linux-i586"
    runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-macosx-universal"
    runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-solaris-amd64"
    runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-solaris-i586"
    runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-windows-amd64"
    runtime "org.jogamp.jogl:jogl-all:2.3.1:natives-windows-i586"
 	testImplementation 'com.ibm.icu:icu4j:50.1'   
}
